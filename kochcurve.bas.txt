10 LAYER 3
20 LET penstate=1
30 LET x=0
40 LET y=64
50 LET dir=0
60 INK 10 
62 PROC PENUP()
65 PROC FORWARD(53)
67 PROC PENDOWN()
70 PROC SNOWFLAKE(600)
75 IF INKEY$ <> "" THEN GO TO 75
80 STOP
90 DEFPROC PENUP()
100 LET penstate=0
110 ENDPROC
120 DEFPROC PENDOWN()
130 LET penstate=1
140 ENDPROC
150 DEFPROC SNOWFLAKE(ls)
160 FOR %i=1 TO 3
170 PROC SNOWSIDE(ls,3)
175 PROC RIGHT(120)
180 NEXT %i
190 ENDPROC
200 DEFPROC SNOWSIDE(ls,le)
210 IF le=0 THEN PROC FORWARD(ls): GOTO 295
220 LOCAL ts
222 LET ts=ls/3
225 LOCAL nl
227 LET nl=le-1
230 PROC SNOWSIDE(ts,nl)
240 PROC LEFT(60)
250 PROC SNOWSIDE(ts,nl)
260 PROC RIGHT(120)
270 PROC SNOWSIDE(ts,nl)
280 PROC LEFT(60)
290 PROC SNOWSIDE(ts,nl)
295 REM here
300 ENDPROC
310 DEFPROC RIGHT(a)
320 LET dir=dir+a
330 ENDPROC
340 DEFPROC FORWARD(s)
350 IF penstate=1 THEN PLOT x,y
360 LET RAD=dir/180* PI
370 LET DX= COS (RAD)*S
380 LET DY= SIN (RAD)*S
390 IF penstate=1 THEN DRAW DX,DY
400 LET X=X+DX
410 LET Y=Y+DY
420 ENDPROC
430 DEFPROC LEFT(a)
440 PROC RIGHT(-a)
450 ENDPROC
460 DEFPROC BACK(S)
470 PROC FORWARD(-S)
480 ENDPROC